apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = GROUP
version = VERSION_NAME

publishing {
    publications {
        Production(MavenPublication) {
            groupId GROUP
            artifactId POM_ARTIFACT_ID
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

def isAndroidProject = project.plugins.hasPlugin('com.android.application') || project.plugins.hasPlugin('com.android.library')

if (isAndroidProject) {
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from project.android.sourceSets.main.java.source
    }
    artifacts {
        archives androidSourcesJar
    }

} else if (project.plugins.hasPlugin("java")) {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocsJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives sourcesJar
        archives javadocsJar
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = POM_NAME
        description = POM_DESCRIPTION
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/summerlly/TypedAdapter.git'
        dryRun = true
        version {
            name = VERSION_NAME
            desc = "version : $VERSION_NAME"
            released = new Date()
        }
    }
}