apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
group = GROUP
version = VERSION_NAME

install {
    repositories.mavenInstaller {
        pom.project {
            name POM_NAME
            description POM_DESCRIPTION
            url POM_URL

            packaging POM_PACKAGING
            groupId GROUP
            artifactId POM_ARTIFACT_ID
            version VERSION_NAME

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection POM_URL
                url POM_URL

            }
            developers {
                developer {
                    name '伯言'
                }
            }
        }
    }
}

def isAndroidProject = project.plugins.hasPlugin('com.android.application') || project.plugins.hasPlugin('com.android.library')

if (isAndroidProject) {
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from project.android.sourceSets.main.java.source
    }
    artifacts {
        archives androidSourcesJar
    }

} else if (project.plugins.hasPlugin("java")) {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocsJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives sourcesJar
        archives javadocsJar
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('BINTRAY_USER')
    key = properties.getProperty('BINTRAY_KEY')
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'typedadapter'
        description = POM_DESCRIPTION
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/summerlly/TypedAdapter.git'
        dryRun = false
        version {
            name = VERSION_NAME
            desc = "version : $VERSION_NAME"
            released = new Date()
        }
    }
}